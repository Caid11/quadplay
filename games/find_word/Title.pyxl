Title
════════════════════════════════════════════════════════════════════════
const INTRO_PRE_FRAMES = 9
const INTRO_BEAT_FRAMES = 35
const INTRO_NUM_BEATS = 6
const INTRO_DURATION = INTRO_PRE_FRAMES + INTRO_BEAT_FRAMES * INTRO_NUM_BEATS

const SLIDE_FRAMES = 40
const HIGHLIGHT_COLOR = #F3C
const SELECTED_BUTTON_COLOR = CURSOR_COLOR

set_random_seed()
const fake_player = make_player()

/* Value for selected_index */
const AVATAR_EDITOR_INDEX = 2
const STATS_INDEX = 3

/* Currently selected button (0-3)*/
let selected_index = GLOBAL

/* Hook for the intro sequence. If ∅ the intro is over */
let intro = ∅

def update_stats_button(pos):
    const selected = selected_index == STATS_INDEX
    
    const radius = 20
    const sprite_args = {sprite: stats_sprite[0][0], pos: pos, z: 100}
    
    if selected:
        draw_round_selection(pos)
        // Outline
        for i < 2:
            draw_sprite({…sprite_args, pos: pos + xy(2i - 1, 0), override_color: #090})    
            draw_sprite({…sprite_args, pos: pos + xy(0, 2i - 1), override_color: #090})    
        draw_sprite({…sprite_args, override_color: #EEE})    
    else:
        // Background
        draw_disk(pos, radius, #FFFC, #DDD, 100)
        draw_sprite(sprite_args)    
        
    draw_text({
        font: score_font, 
        text: "Stats", 
        color: if selected then #FFF else #EEE,
        outline: if selected then rgba(CURSOR_COLOR, 50%) else ∅,
        shadow: #000, 
        pos: pos + xy(0, 27),
        z: 100})

    const region = {pos: pos, shape: "disk", size: radius * xy(2, 2)}        
    if touch.hover and overlaps(touch.hover, region):
        if selected_index ≠ STATS_INDEX:
            play_sound(select_tile_sound, ∅, ∅, 100%)
            selected_index = STATS_INDEX
        
    if ((not intro and
        mode_frames > 2 and 
        ((touch.pressed_a and overlaps(touch.xy, region)) or
         (selected and gamepad_array[0].pressed_a)))
         or DEBUG_STATS):
        push_mode(Stats)        
        
        
    
def draw_round_selection(pos):
    draw_disk(pos - xy(0, 1), 20.5, ∅, #FFF, 2)
    draw_disk(pos - xy(0, 1), 21.5, CURSOR_COLOR, ∅, -0.5)
    draw_disk(pos - xy(0, 1), 24.5, rgba(CURSOR_COLOR, 25%), ∅, -0.6)
    draw_disk(pos - xy(0, 1), 27.5, rgba(CURSOR_COLOR, 15%), ∅, -0.7)
    
    
    
def update_button(pos, text, index):
    const selected = selected_index == index
    
    const BUTTON_SIZE = xy(132, 24)
    
    if selected:
        // Drop shadow
        draw_rect(pos + xy(0, 4), BUTTON_SIZE + 6, #0004, ∅, ∅, 100)
        
        // Highlight
        for 1 ≤ i < 4: 
            draw_rect(pos, BUTTON_SIZE + 2 i, ∅, rgba(lerp(#FFF, SELECTED_BUTTON_COLOR, (i + 1) / 4), 50%), ∅, 100)
        draw_rect(pos, BUTTON_SIZE, SELECTED_BUTTON_COLOR, #FFF, ∅, 100)
        
    else:
        // Drop shadow
        draw_rect(pos + xy(0, 2), BUTTON_SIZE, #0003, ∅, ∅, 100)
        
        // Background
        draw_rect(pos, BUTTON_SIZE, #FFFD, #666, ∅, 100)
        
    draw_text({
        text: text, 
        font: score_font,
        pos: pos + xy(0, 1),
        color: if selected then #FFF else #000,
        outline: if selected then #090 else ∅,
        z: 101})
    
    const rect = {pos: pos, size: BUTTON_SIZE}
    if touch.hover and overlaps(touch.hover, rect):
        if index ≠ selected_index:
            play_sound(select_tile_sound)
            selected_index = index
    
    
    if ((not intro and
        mode_frames > 2 and 
        ((touch.pressed_a and overlaps(touch.xy, rect)) or
         (selected and gamepad_array[0].pressed_a)))
         or (DEBUG_INTERMISSION and index == GLOBAL)):
        game_type = index
        
        // When did the previous game start?
        const prev_start = prev_global_game_utc_start_time()
        
        // Did the previous game finish (i.e., there is no current game)
        const prev_game_finished = utc_time ≥ prev_start + GLOBAL_GAME_TIME
                
        // Is the next game about to begin? If they are within 2 seconds of the next
        // game just send them to the game a little early, it isn't a huge
        // advantage and avoids any race condition
        const next_game_imminent = utc_time > prev_start + GLOBAL_PERIOD - 2
        
        // How much time is left in the current game?
        const curr_game_remaining_time = max((prev_start + GLOBAL_GAME_TIME) - utc_time, 0)

        /*        
        debug_print(
            "prev_start =", prev_start,
            "\nprev_game_finished =", prev_game_finished,
            "\nnext_game_imminent =", next_game_imminent,
            "\ncurr_game_remaining_time =", curr_game_remaining_time) */
        
        if ((index == GLOBAL and
            (prev_game_finished or curr_game_remaining_time < GLOBAL_JOIN_MIN_REMAINING_TIME) and
            not next_game_imminent) or DEBUG_WAITING_ROOM):
            // There is no game currently running because the global game is in 
            // the GameOver state, or about to be so. Send this player to the waiting room
            // so that they aren't bored. 
            in_waiting_room = true
        
        play_sound(blip_sound)
        set_mode(GenerateBoard) because "Chose game type"
        


def bounce_row(y, sound default select_tile_sound, color default TILE_CENTER_SELECTED_COLOR):
    play_sound(sound)
    board_deselect_all(fake_player.board, fake_player)
    fake_player.board.selection_color = color
    for x < 4:
        push(fake_player.board.selection, xy(x, y))
        fake_player.board.tile[x][y].selected = mode_frames


def bounce_row1(): bounce_row(0)

def bounce_row2(): bounce_row(1)

def bounce_row3(): bounce_row(2)

def bounce_row4(): bounce_row(3)

def highlight_random_row():
    bounce_row(random_integer(0, ROWS - 1), word_sound, HIGHLIGHT_COLOR)

    
def intro_cleanup():
    intro = ∅ 
    
    
def ease_out(f):
    return sign(f) * pow(abs(f), 1.3)
    

/* Jump to the end of the intro */
def skip_intro():
    highlight_random_row()
    remove_frame_hook(intro)
    intro = ∅


// Initialize the fake board
player_reset(fake_player, SOLO, "FIND WORD ALLL NITE")


enter
────────────────────────────────────────────────────────────────────────
set_background(#000)
set_hooks()

fake_player.board.selection_color = TILE_CENTER_SELECTED_COLOR
board_deselect_all(fake_player.board, fake_player)

def sound():
    play_sound(times_up_sound)
    
intro = sequence(
    INTRO_PRE_FRAMES, 
    bounce_row1, INTRO_BEAT_FRAMES,
    bounce_row2, INTRO_BEAT_FRAMES,
    bounce_row3, INTRO_BEAT_FRAMES,
    bounce_row4, INTRO_BEAT_FRAMES,
    highlight_random_row,
    SLIDE_FRAMES + 40,
    sound,
    100,
    intro_cleanup)


pop_mode from AvatarEditor
────────────────────────────────────────────────────────────────────────
intro_cleanup()


frame
────────────────────────────────────────────────────────────────────────
draw_text({
        pos: xy(0, 0),
        font: tile_point_font,
        text: "v" + CREDITS.version + "+" + QUADPLAY_INFO.version,
        color: #777,
        y_align: "top",
        x_align: "left",
        z: 1})

draw_background_words()

// Spin after the intro
const θ = -0.2° max(0, mode_frames - INTRO_DURATION)
const pos = ½ SCREEN_SIZE
player_update_board(fake_player, pos, θ, false)
draw_rect(pos, board_pixel_size() + 22, fake_player.board.selection_color, ∅, θ, -1) 

// How far to slide the buttons, 0 if the intro has been skipped
const slide = if intro then (-3 min(ease_out(mode_frames - INTRO_DURATION - SLIDE_FRAMES), 0)) else 0

update_button(pos + xy(slide, -30), "Solo", 0)
update_button(pos - xy(slide, -30), "Global", 1)

// Logo
draw_sprite({sprite: logo_sprite, pos: xy(SCREEN_SIZE.x - 32 + slide, 20), z: -0.6})

// Avatar
local:
    const pos = xy(32 - slide, SCREEN_SIZE.y - 36)
    const bounds = {pos: pos, size: xy(32, 32)}
    
    if touch.hover and overlaps(touch.hover, bounds) and selected_index ≠ AVATAR_EDITOR_INDEX:
        play_sound(select_tile_sound, ∅, ∅, -100%)
        selected_index = AVATAR_EDITOR_INDEX
        
    if selected_index == AVATAR_EDITOR_INDEX:
        draw_round_selection(pos)
    else:
        draw_disk(pos, 20, rgba(player.avatar.color, 75%), 75% player.avatar.color)
        
    avatar_draw_large(player.avatar, pos)
    draw_text({text: "Level " + player.avatar.level, z: 102, pos: pos - xy(0, 26), color: player.avatar.color, outline: #0008, font: leaderboard_font})
    
    if (not intro and
        ((touch.pressed_a and overlaps(touch.xy, bounds)) or
         (player.gamepad.pressed_a and selected_index == AVATAR_EDITOR_INDEX))):
        push_mode(AvatarEditor) because "Selected avatar"
    
// Stats button
local:
    const pos = xy(SCREEN_SIZE.x - 32 + slide, SCREEN_SIZE.y - 36)
    update_stats_button(pos)


if gamepad_array[0].xx or gamepad_array[0].yy:
    if intro: skip_intro()
    // Change selection
    selected_index = loop(selected_index + (gamepad_array[0].xx or gamepad_array[0].yy), STATS_INDEX + 1)
    play_sound(select_tile_sound)


if any_button_press() or touch.pressed_a:
    skip_intro()

