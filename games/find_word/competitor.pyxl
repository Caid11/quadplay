/* Information about other players online (or virtual ones) used by Intermission */

/* 
   ```
   {
        avatar
        text
        badges
   
        num_rare
        longest_size
        longest_text
        best_word_points
        best_word_text
        best_free_word_points
        best_free_word_text
        best_rare_points
        best_rare_text
        word_list
        free_word_list
        score
    }```
   
   
   `word_list` is Q-encoded. `text` values are Q-decoded.
   
   */
const competitor_array = []

let player_competitor

// Competitors winning each award
let best_word_winner
let best_rare_winner
let longest_winner
let most_words_winner


/* Are they a friend of the player_competitor */
def competitor_is_friend(competitor):
    todo("Remove when we have a friending UI")
    // For now, friend all humans. Bots have a `base_ability` property
    return ((competitor.avatar.id == MOO_ID and player.has_ability["Friend of Moo"]) or
            (competitor.avatar.id == CAF_ID and player.has_ability["Friend of caf"]) or    
            ((competitor ≠ player_competitor) and player.friends[competitor.avatar.id]) or
             (not competitor.avatar.base_ability))
        


/* Generate a stable set of bots with a range of abilities, and 
   produce their scores and word lists for the current `player.board`. */
def insert_bots(competitor_array, target_count):
    const avatar_set = [
{
name: "THX",
sprite: avatar_sprite[13][4],
base_ability: 0.22,
short_word_preference: 0.16354711555742432,
first_hour: 11,
last_hour: 19
},
{
name: "o_o",
sprite: avatar_sprite[20][14],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 17,
last_hour: 7
},
{
name: "AMS",
sprite: avatar_sprite[37][14],
base_ability: 0.3,
short_word_preference: 0.3227786173178901,
first_hour: 22,
last_hour: 10
},
{
name: "NT",
sprite: avatar_sprite[17][6],
base_ability: 0.08139577593236193,
short_word_preference: 0.6744168962705523,
first_hour: 8,
last_hour: 12
},
{
name: "I♥U",
sprite: avatar_sprite[3][20],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 19,
last_hour: 4
},
{
name: "jen",
sprite: avatar_sprite[39][8],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 7,
last_hour: 11
},
{
name: "411",
sprite: avatar_sprite[24][18],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 21,
last_hour: 10
},
{
name: "RLL",
sprite: avatar_sprite[33][24],
base_ability: 0.21281131767993983,
short_word_preference: 0.2136182478620353,
first_hour: 15,
last_hour: 2
},
{
name: "CJ",
sprite: avatar_sprite[39][24],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 15,
last_hour: 0
},
{
name: "Bob",
sprite: avatar_sprite[28][26],
base_ability: 0.09596281621507843,
short_word_preference: 0.6161487351396863,
first_hour: 0,
last_hour: 7
},
{
name: "TTT",
sprite: avatar_sprite[39][16],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 2,
last_hour: 9
},
{
name: "HCD",
sprite: avatar_sprite[39][6],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 12,
last_hour: 20
},
{
name: "GEW",
sprite: avatar_sprite[39][2],
base_ability: 0.3037478105091114,
short_word_preference: 0.45393640967962884,
first_hour: 11,
last_hour: 15
},
{
name: "jr",
sprite: avatar_sprite[9][18],
base_ability: 0.02798352169998617,
short_word_preference: 0.8880659132000553,
first_hour: 18,
last_hour: 6
},
{
name: "FLA",
sprite: avatar_sprite[9][16],
base_ability: 0.02,
short_word_preference: 0.92,
first_hour: 1,
last_hour: 7
},
{
name: "Lou",
sprite: avatar_sprite[8][18],
base_ability: 0.0711575915744297,
short_word_preference: 0.7153696337022812,
first_hour: 6,
last_hour: 12
},
{
name: "RON",
sprite: avatar_sprite[8][16],
base_ability: 0.034604881436825705,
short_word_preference: 0.8615804742526971,
first_hour: 4,
last_hour: 18
},
{
name: "CPU",
sprite: avatar_sprite[21][2],
base_ability: 0.08,
short_word_preference: 0.92,
first_hour: 12,
last_hour: 1
}]

    // Make results for the board stable so that all human players see the
    // bots doing the same things
    const match_rng = make_random(player.board.seed)
    
    // Used for finding legal substrings
    const word_table = {}
    for word in player.board.word_list:
        if dictionary[word]:
            word_table[word] = 1
    
    // Used for determining which players are active
    const hour = utc_time().hours
    const minute = utc_time().minute
    
    // Shift everybody's ability by this amount
    const match_shift = match_rng(-10%, 0%)
    
    def make_bot_competitor(avatar, id):
        const competitor = {
            avatar: avatar,
            score: 0,
            free_list: []}
    
        competitor.avatar.id = id
    
        // Bots face left
        competitor.avatar.sprite = competitor.avatar.sprite.x_flipped
        competitor.avatar.color = avatar_compute_color(competitor.avatar.sprite)
        competitor.avatar.level = max(1, ⌈70 competitor.avatar.base_ability⌉)
        
        // Make stable how good this player is overall, and then add noise per game.
        // Apply that as a fraction of all words possible
        const base_ability = competitor.avatar.base_ability
        const find_percentage = min(28%, match_rng(3%^⅓, 30%^⅓)³ + base_ability)
        const words_found = clamp(⌊size(player.board.word_list) * find_percentage⌋, 5, 55)
        const short_word_preference = competitor.avatar.short_word_preference
        
        // Is this particular competitor available right now?         
        if competitor.avatar.last_hour > competitor.avatar.first_hour:
            if hour < competitor.avatar.first_hour or hour > competitor.avatar.last_hour: return
        else:
            // Play hours wrapped around the clock
            if hour > competitor.avatar.last_hour and hour < competitor.avatar.first_hour: return
        
        // Make a word list for this competitor and score it.
        // Higher chance of discovering shorter words via rejection sampling.
        
        const word_set = {}
        def maybe_add_word(word):
            assert(word)
            // Accept words that this competitor hasn't found with
            // decreasing probability as the word gets longer than 3 letters.
            // Players with higher base percentages also are better at long words.
            if (not sensitive[lowercase(word)] and
                not word_set[word] and
                match_rng() ≤ (2.8 base_ability + 23%) / pow(max(size(word) - 2, 1), 0.4 + 3 short_word_preference)):
                // Ensure that if a word is found, all substrings are found
                for 3 ≤ s ≤ size(word):
                    const w = slice(word, 0, s)
                    if word_table[w]:
                        word_set[w] = 1
        
        // Special case for Moo, who always finds *all* cow-related words
        if competitor.avatar.id == MOO_ID:
            for word in MOOCABULARY:
                if not word_set[word] and contains(player.board.word_list, word):
                    word_set[word] = 1
                    
        let tries = 0        
        // Special case for caf, who sometimes finds a great word, finds all
        // three-letter cow words, and otherwise finds nothing
        if competitor.avatar.id == CAF_ID:
            caf_ai(word_set, player.board.word_list)
        else:
            // Pick up a bunch of cheap words first
            const short_word_length = if competitor.avatar.id == MOO_ID then 3 else 4
            while size(word_set) < clamp(2 short_word_preference, 10%, 90%) * words_found and tries < 2 words_found:
                ++tries
                const word = random_value(player.board.word_list, match_rng)
                if size(word) ≤ short_word_length:
                    maybe_add_word(word)
    
        // Moo cannot find non-cow words greater than 4 letters    
        if competitor.avatar.id ≠ MOO_ID and competitor.avatar.id ≠ CAF_ID:
            tries = 0
            while size(word_set) < words_found and tries < 2 words_found:
                ++tries
                maybe_add_word(random_value(player.board.word_list, match_rng))
    
        competitor.word_list = keys(word_set)
                
        sort(competitor.word_list, size_comparator, true)
        sort(competitor.free_list, size_comparator, true)
        
        for word at i in competitor.word_list:
            competitor.score += score_word(word)

        return competitor
    
    // Moo is a special case
    // ALWAYS insert Moo if the player has the See Moo ability, otherwise insert Moo if
    // it is an odd 10 minute interval right now    
    if player.has_ability["See Moo"] or (⌊minute / 10⌋ mod 2):
        push(competitor_array,
            make_bot_competitor({
                    name: "Moo",
                    sprite: avatar_sprite[34][22],
                    base_ability: 0.1,
                    short_word_preference: 0.80,
                    first_hour: 0,
                    last_hour: 22},
                MOO_ID))
    
    if DEBUG_CAF or player.has_ability["See caf"]:// and ((hour + ⌊minute / 7⌋) mod 2 == 0)) or DEBUG_CAF:
        push(competitor_array,
            make_bot_competitor({
                    name: "caf",
                    sprite: avatar_sprite[33][22],
                    base_ability: 0.0,
                    short_word_preference: 0.80,
                    first_hour: 0,
                    last_hour: 23},
                CAF_ID))
    
    let c = 0
    while size(competitor_array) < target_count and avatar_set[c]:       
        const competitor = make_bot_competitor(avatar_set[c], "BOT_" + c)
        ++c
        push(competitor_array, competitor)



/* Special case AI for the caf bot. `word_list` is the input,
   `word_set` is the output found words */
def caf_ai(word_set, word_list):
    if ξ < 10% or DEBUG_CAF:            
        const word = word_list[random_integer(max(0, size(word_list) - 6), size(word_list) - 1)]
        if word and not word_set[word]:
            // Find this word
            word_set[word] = 1
            
    // Look for short cow words
    for word in MOOCABULARY:
        if size(word) ≤ 3 and not word_set[word] and contains(word_list, word):
            word_set[word] = 1

    // Find all words with "CAF" as a substring
    for word in player.board.word_list:
        if contains(word, "CAF") and not word_set[word]:
            word_set[word] = 1



/* Convert the local player into a competitor */
def make_player_competitor():
    const competitor = {
        avatar: player.avatar,
        score: player.score,
        word_list: [],
        free_list: []}
    
    for entry in player.history.list:
        push(competitor.word_list, entry.word)
        
    for entry in player.history.free:
        push(competitor.free_list, entry.word)
    
    return competitor



/* Called from `Intermission.enter` */
def competitor_analyze(competitor_array):
    // How many players have each word; used for 
    // identifying rare words
    const count = {}
    
    // Compute everyone's best results
    for competitor in competitor_array:
        competitor.badges = ""
        
        // Censor names by slicing off the 2nd letter, if inappropriate,
        // and replacing common letter substitutions to prevent casual
        // looks-like thwarting
        if offensive[lowercase(replace(competitor.avatar.name, LOOKS_LIKE))] or sensitive[lowercase(competitor.avatar.name)]:
            competitor.avatar.name = competitor.avatar.name[0] + competitor.avatar.name[2] + " "

        competitor.best_free_word_points = 0
        competitor.best_free_word_text = ""
        for word in competitor.free_list:
            const points = score_word(word)
            if points > competitor.best_free_word_points:
                competitor.best_free_word_points = points
                competitor.best_free_word_text = q_decode(word)
            

        competitor.best_word_points = 0
        competitor.longest_size = 0
        for word in competitor.word_list:
            // Increment the count
            count[word] = (count[word] default 0) + 1
            
            const points = score_word(word)
            const text = q_decode(word)
            
            if (points > competitor.best_word_points) or (points == competitor.best_word_points and text > competitor.best_word_text):
                competitor.best_word_points = points
                competitor.best_word_text = text
                competitor.text = text
        
            if (size(text) > competitor.longest_size) or (size(text) == competitor.longest_size and text > competitor.longest_text):
                competitor.longest_size = size(text)
                competitor.longest_text = text
    
    // Count at which a word is rare
    const rare_threshold = max(1, floor(size(competitor_array) / 6))

    // Find the best rare word for each competitor
    for competitor in competitor_array:
    
        competitor.num_rare = 0
        competitor.best_rare_points = 0
        competitor.best_rare_text = 0
    
        for word in competitor.word_list:
            if count[word] <= rare_threshold:
                const points = score_word(word)
                const text = q_decode(word)
                
                // This word has appeared very few times
                ++competitor.num_rare
                
                if points > competitor.best_rare_points or (points == competitor.best_rare_points and text > competitor.best_rare_text):
                    competitor.best_rare_points = points
                    competitor.best_rare_text = text
    
    // Update the history rare fields for the player competitor
    // as these are used for display. We don't need this for others
    for entry in player.history.list:
        if count[entry.word] ≤ rare_threshold:
            entry.rare = true
    
    competitor_assign_awards(competitor_array)
    

    
/* Helper for `competitor_analyze()`. Sets the `_winner` globals
   and assigns the `competitor.badges` strings. */
def competitor_assign_awards(competitor_array):
    // Rank all competitors by score
    sort(competitor_array, "score", true)
    
    // Find the winner in each category. Ties are broken
    // by total score by running in order after that sort
    best_word_winner  = competitor_array[0]
    best_rare_winner  = competitor_array[0]
    longest_winner    = competitor_array[0]
    most_words_winner = competitor_array[0]
    
    for competitor in competitor_array:
        if competitor.best_word_points > best_word_winner.best_word_points:
            best_word_winner = competitor
            
        if competitor.best_rare_points > best_rare_winner.best_rare_points:
            best_rare_winner = competitor

        if competitor.longest_size > longest_winner.longest_size:
            longest_winner = competitor
            
        if size(competitor.word_list) > size(most_words_winner.word_list):
            most_words_winner = competitor



    // Now loop again and give badges, including for ties:
    for competitor in competitor_array:
        // Put in the same order as the award stages
        if competitor.best_word_points == best_word_winner.best_word_points:
            competitor.badges += BADGE_TABLE.best_word
            
        if competitor.best_rare_points == best_rare_winner.best_rare_points:
            competitor.badges += BADGE_TABLE.best_rare

        if competitor.longest_size == longest_winner.longest_size:
            competitor.badges += BADGE_TABLE.longest

        if size(competitor.word_list) == size(most_words_winner.word_list):
            competitor.badges += BADGE_TABLE.most_words
    
