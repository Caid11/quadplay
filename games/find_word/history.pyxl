
/* Mutates `pos`, returns `max_width`. Called from `_player_update()`. */
def history_draw(history, pos, max_height):
    
    const start_y = pos.y
    
    let max_width = 0
    for entry at e in history.list:
        // Wrap into multiple columns if this element will
        // go off the page. Do this here so that we never
        // reset right before the end
        if pos.y > max_height - 8:
            pos.y = start_y
            pos.x += max_width + HISTORY_COLUMN_SEPARATION
            max_width = 0
            
        const emphasize = max(0, entry.emphasize_frame - game_frames)
        
        const wiggle = xy(oscillate(18% emphasize, -1, +1) - sign(emphasize), 0)
    
        // Save for animation later
        entry.pos = transform_ws_to_ss(pos)
        
        // Hide while animation is playing for this word, but
        // draw invisibly so we get bounds
        const show = game_frames > entry.reveal_frame
        const color = entry.color
        const outline = if emphasize then #FFF else entry.outline
        
        let text = entry.text
        
        // Emphasize unique values when on the Intermission mode.
        // For other modes, unique will never be true
        if entry.unique: text += "âœ“"
        
        const bounds = draw_text({
            // Bounce if emphasized
            pos: pos + wiggle,
            font: if emphasize then history_emphasis_font else history_font,
            x_align: "left",
            y_align: "top",
            color: if show then color else #0000,
            outline: if show then outline else #0000,
            z: emphasize,
            text: text})

        // Track if this makes the column larger
        max_width = max(max_width, text_width(history_font, text))
        pos.y += history_font.line_height + 1
        
    return max_width
        