/* Draws a tile, adjusting fonts appropriately for the size */
def tile_draw(tile, pos, is_cursor_tile, angle, selection_color, tile_size):
    // Shrink by 5 at 44
    const inset = ⌊tile_size.x / 9⌋
    const center_size = ceil(tile_size - inset, ½)
    const subscript_offset = angle_to_xy(-45° - angle) * ⅓ center_size.x
    let radius = ½ center_size.x
    
    if tile.selected:
        // Bounce if just selected
        if tile.selected > game_frames - 12:
            radius += ⅕ oscillate(tile.selected - game_frames, 0, 6) + 0.8
    
    if not tile.selected or is_cursor_tile:
        // Background rect
        draw_rect(pos, tile_size, if is_cursor_tile then CURSOR_COLOR else TILE_COLOR, if is_cursor_tile then #0C0 else ∅) 
    
    draw_disk(
        pos,
        radius,
        if tile.selected then selection_color else TILE_CENTER_COLOR,
        ∅, 
        TILE_CENTER_Z)

    const text_color = if tile.selected then SELECTED_TEXT_COLOR else TEXT_COLOR
    
    // Label
    draw_text({
        pos: pos, 
        font: if tile_size.x > 24 then tile_font else tile_small_font, 
        text: if tile.letter == "Q" then "Qu" else tile.letter,
        color: text_color,
        z: TILE_LETTER_Z})
    
    // Points
    if not tile.selected and tile_size.x > 18:
        draw_text({
            pos: pos + subscript_offset, 
            font: tile_point_font, 
            text: LETTER_POINT_TABLE[tile.letter], 
            color: #AAA,
            z: TILE_LETTER_Z})