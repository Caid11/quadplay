GameOver
════════════════════════════════════════════════════════════════════════


def points_order(A, B):
    return B.points - A.points


enter
────────────────────────────────────────────────────────────────────────
board_deselect_all(player.board, player)

sort(player.history.list, points_order)


frame
────────────────────────────────────────────────────────────────────────

const left = if landscape() then ⅓ else ¼
const right = 100% - left

// Score
draw_text({
    font: score_font,
    text: format_number(player.score, "commas") + " pts",
    outline: #000,
    shadow: #000,
    pos: xy(left * SCREEN_SIZE.x, 24),
    color: #FFF})

draw_text({
    font: history_font,
    text: "" + size(player.history.list) + "/" + player.board.word_count + " words found",
    pos: xy(left * SCREEN_SIZE.x, 38),
    shadow: #000,
    color: #FFF})

todo("Global sync game start")

// Board
for y < ROWS:
    for x < COLS:
        const pos = xy(right * SCREEN_SIZE.x, 30) + 12 xy(x - 1.5, y - 1.5)
        draw_disk(pos, 5, #FFF)
        
        const letter = player.board.tile[x][y].letter
        draw_text({
            pos: pos,
            font: history_font,
            color: #000,
            text: if letter == "Q" then "Qu" else letter})


// History
history_draw(player.history, xy(4, 60), SCREEN_SIZE.y - 30)

// Countdown
draw_text({
    font: score_font,
    text: format_number(max(INTERMISSION_MINUTES * 60 - mode_frames / 60, 0), "timer") + " until next game…",
    pos: xy(½ SCREEN_SIZE.x, SCREEN_SIZE.y - 10),
    color: #FFF})

if mode_frames ≥ 3600 INTERMISSION_MINUTES:
    set_mode(Play) because "Timer"

