Intermission
════════════════════════════════════════════════════════════════════════

todo("* Implement awards")
todo("* Make SOLO version")
todo("Force GLOBAL to SOLO version when nobody else is there")
todo("Mark unique words in history")

// History      Award
//   :
//   :           Rank
//   :            :
//                :
//
//              Suggestion
// 
// [ Button/Timer ]

// {avatar, score, word_list, representative_text}
// word_list is Q-encoded
const competitor_array = []

let end_time
let unfound_word
let unfound_word_array

def points_order(A, B):
    return B.points - A.points
    
    
def word_comparator(entry, word):
    return entry.word == word
        
    
def length_comparator(A, B):
    return size(A) - size(B)


def swap(array, i, j):
    const temp = array[i]
    array[i] = array[j]
    array[j] = temp


enter
────────────────────────────────────────────────────────────────────────
set_background(#333)
board_deselect_all(player.board, player)
//debug_print(player.board.word_list)
sort(player.history.list, points_order)

if game_type == GLOBAL:
    end_time = next_global_game_utc_start_time()


unfound_word_array = []
for word in player.board.word_list:
    if not sensitive[lowercase(word)] and not contains(player.history.list, word, word_comparator):
        push(unfound_word_array, word)


// Sort from shortest to longest to emphasize impressive words
sort(unfound_word_array, length_comparator)

// Now swap some of the other words towards the back so that
// the player also feels like they could have gotten these
if size(unfound_word_array) > 10:
    for i < 3:
        swap(unfound_word_array, random_integer(0, size(unfound_word_array) - 8), size(unfound_word_array) - 1 - random_integer(1, 6))

board_deselect_all(player.board)

// Convert the local player into a competitor
local:
    let best_word_points = 0
    const competitor = {
        avatar: player.avatar,
        score: player.score,
        me: true,
        text: ∅,
        word_list: []}
        
    push(competitor_array, competitor)
        
    for entry in player.history.list:
        push(competitor.word_list, entry.word)
        if entry.points > best_word_points:
            best_word_points = entry.points
            competitor.text = q_decode(entry.word)


// Make some bogus competitors
todo("Replace with real in a GLOBAL game")
for i < 10:
    const sprite = avatar_sprite[4][2i]
    const word = random_value(player.board.word_list)
    const competitor = {
        avatar: {sprite: sprite, color: avatar_compute_color(sprite), name: random_value(ALPHABET) + random_value(ALPHABET) + random_value(ALPHABET)},
        score: 42 + 11 score_word(word), 
        word_list: [],
        text: q_decode(word)}
    push(competitor_array, competitor)   
    
sort(competitor_array, "score", true)

    
frame
────────────────────────────────────────────────────────────────────────

// History
preserving_transform:
    set_clip(xy(0,0), xy(SCREEN_SIZE.x - 120, SCREEN_SIZE.y)) 
    history_draw(player.history, xy(2, 1), SCREEN_SIZE.y - 22)


const left = 50%
const right = 75%

// Score
/*
draw_text({
    font: score_font,
    text: format_number(player.score, "commas") + " pts",
    outline: #000,
    shadow: #000,
    pos: xy(left * SCREEN_SIZE.x, 18),
    color: #FFF})
*/

// Awards (Longest, Best Word, Best Unique, High Score, Most Words)
preserving_transform:
    const avatar = player.avatar
    set_transform(xy(SCREEN_SIZE.x - 95, 34))
    
    draw_text({font: history_font, text: "Best Word", color: #FFF, pos: xy(0, -29)})
    avatar_draw_large(avatar, xy(0, -7))    

// Board
preserving_transform:
    set_transform(xy(SCREEN_SIZE.x - 32, 34))
    
    if mode_frames mod 420 == 100:
        // Choose the next unfound word
        unfound_word = pop(unfound_word_array)
        
        // Select this word
        board_deselect_all(player.board)
        player.board.selection = board_find_unencoded_word(player.board, unfound_word)
        player.board.selection_color = #E1B
        
        // Select these letters
        for index in player.board.selection:
            player.board.tile[index.x][index.y].selected = mode_frames
    
        
    for y < ROWS:
        for x < COLS:
            const tile = player.board.tile[x][y]
            tile.pos = 12 xy(x - 1.5, y - 1.5)
            
            draw_disk(tile.pos, 5, if tile.selected then player.board.selection_color else #FFF)
            
            draw_text({
                pos: tile.pos,
                font: history_font,
                color: if tile.selected then #FFF else #000,
                text: if tile.letter == "Q" then "Qu" else tile.letter})
    
    if size(player.board.selection) > 0:
        // Draw word
        draw_text({
            font: history_font,
            text: "\"" + unfound_word + "\"",
            pos: xy(0, -29),
            shadow: #000,
            color: player.board.selection_color})
            
        // Draw connectors
        for 1 ≤ i < size(player.board.selection):
            const prev = player.board.selection[i - 1]
            const curr = player.board.selection[i]
            draw_line(player.board.tile[prev.x][prev.y].pos, player.board.tile[curr.x][curr.y].pos, player.board.selection_color, -1, 2)
    
    todo("Put # found at end of history")
    /*
    draw_text({
        font: history_font,
        text: "" + size(player.history.list) + "/" + player.board.word_count + " found",
        pos: xy(0, 30),
        shadow: #000,
        color: #FFF})*/
        
        
// Rank
preserving_transform:
    set_clip(xy(0, 0), xy(SCREEN_SIZE.x, SCREEN_SIZE.y - 22))
    set_transform(xy(SCREEN_SIZE.x - 104, 80))    

    const RANK_SPACING_Y = 16
    for competitor at i in competitor_array:
        const pos = xy(-3, i * RANK_SPACING_Y)
                
        // Highlight
        if competitor.me:
            draw_corner_rect(pos - xy(10, 8), xy(120, 16), #F3C6, ∅, ∅, -1)
            
        // Animate sprite
        let sprite = competitor.avatar.sprite
        if ⌊mode_frames / 30⌋ mod 2:
            sprite = sprite.spritesheet[sprite.tile_index.x][sprite.tile_index.y + 1]
         
        draw_sprite(sprite.x_flipped, pos)
        draw_text({text: competitor.avatar.name, pos: pos + xy(17, 0), color: competitor.avatar.color})
        draw_text({text: competitor.score, pos: pos + xy(58, 0), x_align: "right", color: #FFF})
        draw_text({font: history_font, text: competitor.text, x_align: "left", pos: pos + xy(68, 0), color: #FFF})

            
// Button/timer
if game_type == SOLO:

    const pos = xy(½ SCREEN_SIZE.x, SCREEN_SIZE.y - 10)
    const size = xy(150, 17)
    draw_rect(pos - xy(0, 1), size, #0F0, #FFF)
    draw_text({
        font: score_font,
        text: "Start New Game",
        outline: #090,
        pos: pos,
        color: #FFF})
        
    if (any_button_press(gamepad_array[0]) or (touch.aa and overlaps(touch.xy, {pos: pos, size: size}))) and mode_frames > 15:
        set_mode(GenerateBoard) because "Button press"
        
else:
    // Countdown
    const time_left = max(end_time - utc_time, 0)
    const timer_pos = xy(½ SCREEN_SIZE.x, SCREEN_SIZE.y - 10)
    const α = 100% - time_left / (if game_type == SOLO then SOLO_INTERMISSION_TIME else GLOBAL_INTERMISSION_TIME)
    
    draw_corner_rect(xy(0, timer_pos.y - 10), xy(SCREEN_SIZE.x, 19), #000, #FFF)
    draw_corner_rect(xy(1, timer_pos.y - 9), xy(α * (SCREEN_SIZE.x - 2), 17), #0F0)
    
    draw_text({
        font: score_font,
        text: format_number(time_left, "timer") + " until next game…",
        outline: #090,
        pos: timer_pos,
        color: #FFF})
    
    if utc_time ≥ end_time:
        set_mode(GenerateBoard) because "Global timer"

