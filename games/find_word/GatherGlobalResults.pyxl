GatherGlobalResults
════════════════════════════════════════════════════════════════════════

const color = #F3C


def draw_arc(pos, radius, start, stop, color):
    const N = 10
    let prev = pos + angle_to_xy(start) * radius
    for i < N:
        const θ = lerp(start, stop, i / (N - 1),)
        const curr = pos + radius * angle_to_xy(θ)
        draw_line(prev, curr, color)
        prev = curr


def draw_globe(pos, radius):
    draw_disk(pos, radius, ∅, color)
    
    const parallels = 7
    for 0 < p < parallels:
        const θ = 180° (p + 1) / (parallels + 1) 
        draw_arc(pos, xy(1, cos θ) * radius, 12°, 168°, color)
    
    const meridians = 15
    for m < meridians:
        const ϕ = loop(360° * m / meridians + 1° game_frames, 360°)
        if ϕ < 180°:
            draw_arc(pos, xy(cos ϕ, 1) * radius, 90°, 270°, color)

enter
────────────────────────────────────────────────────────────────────────
remove_all(competitor_array)

player_competitor = make_player_competitor()
push(competitor_array, player_competitor)

share_results_with_network(player_competitor)
  
// Make some bots to fill out the competitor list if offline or
// there are too few players
insert_bots(competitor_array, ⌊4 noise(1, local_time().month + local_time().day + local_time().hour)⌋ + random_integer(8, 15))

end_time = next_global_game_utc_start_time() - GLOBAL_INTERMISSION_TIME


frame
────────────────────────────────────────────────────────────────────────
const utc_time = utc_now()
const start_time = end_time - GATHER_TIME_SECONDS
const τ = min((utc_time - start_time) / GATHER_TIME_SECONDS, 100%)

update_network()

set_background(GLOBAL_BACKGROUND_COLOR)

draw_text({
    text: "Gathering Global Scores…",
    font: score_font,
    color: color,
    y_align: "center",
    pos: ½ SCREEN_SIZE})

const y = ½ SCREEN_SIZE.y + 20
const w = 200
draw_corner_rect(xy(½ SCREEN_SIZE.x - ½ w, y), xy(w * τ, 10), color)
draw_corner_rect(xy(½ SCREEN_SIZE.x - ½ w, y), xy(w, 10), ∅, color)

draw_globe(xy(½ SCREEN_SIZE.x, ½ SCREEN_SIZE.y - 40), 20)

if utc_time ≥ end_time:
    set_mode(GlobalIntermission) because "Timer"
    
    
leave
────────────────────────────────────────────────────────────────────────
disconnect_from_network()
competitor_analyze(competitor_array)

todo("Trim down when there are very many competitors, keeping the top 2, the bottom 3, and 3 around the player")
