_ConfirmDialog
════════════════════════════════════════════════════════════════════════

const windowBackgroundColor = #135
const windowBorderColor     = #fff
const text_color            = #fff
const text_shadow_color     = #0006
const promptColor           = #4DF
const selectedColor         = #f
const unselectedColor       = #b

const openFrames            = 8

let title
let message
let callback
const sz = xy(0, 0)

const choiceArray = ["No", "Yes"]
let choiceIndex

let font = _font9
if SCREEN_SIZE.y <= 64: font = _font5
else if SCREEN_SIZE.y <= 112: font = _font8


enter(choice)
────────────────────────────────────────────────────────────────────────
title = choice.text
message = if is_string(choice.confirm) then choice.confirm else ""
callback = choice.callback

choiceIndex = 0
play_sound(_openUISound)

sz.x = mid(SCREEN_SIZE.x - 2, 150% * max(text_width(font, title), text_width(font, message)), SCREEN_SIZE.x / 2)
sz.y = font.line_height * (5 + (if message == "" then 0 else 3))


frame
────────────────────────────────────────────────────────────────────────
draw_previous_mode()
set_transform(xy(0, 0), xy(1, 1), 2048.6, 1)

const windowSize = round(sz * clamp((mode_frames + 1) / openFrames, 0%, 100%))
draw_rect(½ SCREEN_SIZE, windowSize, windowBackgroundColor, windowBorderColor)

// Window ambient occlusion
draw_rect(½ SCREEN_SIZE, windowSize + 2, ∅, #0007)
draw_rect(½ SCREEN_SIZE, windowSize + 4, ∅, #0003)
if SCREEN_SIZE.y >= 112: draw_rect(½ SCREEN_SIZE, windowSize + 6, ∅, #0001)

if mode_frames > max(15, openFrames):
   for control in gamepad_array:
      if control.qq or control._pp or (control.aa and choiceIndex == 0):
         // Cancel
         play_sound(_cancelUISound)
         pop_mode(∅)
      else if control.aa:
         // Accept option
         play_sound(_acceptUISound)
         pop_mode(callback)
      else if control.xx:
         const prev = choiceIndex
         choiceIndex = clamp(choiceIndex + control.xx, 0, 1)
         play_sound(if prev == choiceIndex then _denyUISound else _moveUISound)

         
if mode_frames > openFrames:
   draw_text({font: font, text: title + "?", pos: ½ SCREEN_SIZE + xy(0, - ½ windowSize.y + 150% font.line_height), color: text_color, shadow:text_shadow_color, x_align: "center", y_align: "center"})

   if message != "":
       draw_text({font: font, text: message, pos: ½ SCREEN_SIZE, color: text_color, x_align: "center", y_align: "center", wrap_width: 90% windowSize.x})

   for i < 2:
      const active      = choiceIndex == i
      const bright      = ½ (1 + cos(mode_frames / 9))
      const color       = if active then lerp(promptColor, #fff, 50% bright) else unselectedColor
      const outline     = if active then rgba(1,1,1, 25% bright) else ∅
      const offset      = xy(0, if active then -1 else 0)
      const shadow      = if active then text_shadow_color * 50% else text_shadow_color
      const pos         = ½ SCREEN_SIZE + xy((2i - 1) * ⅕ windowSize.x, ½ windowSize.y - 150% font.line_height)
      
      draw_text({font: font, text: choiceArray[i], pos: pos + xy(0, 1), color: shadow, x_align: "center", y_align: "center"})
      draw_text({font: font, text: choiceArray[i], pos: pos + offset, color: color, outline: outline, x_align: "center", y_align:"center"})
