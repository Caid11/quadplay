_GameCredits
════════════════════════════════════════════════════════════════════════

const window_background_color = #135
const window_border_color     = #fff
const text_color              = #fff
const text_shadow_color       = #0006
const prompt_color            = #4DF
const selected_color          = #f
const unselected_color        = #b

const creditOrder = ["Game", "Sprite", "Font", "Sound", "Code", "Quadplay"]

let old_transform
let old_clip
let old_post_effects
let old_background

let sectionFont = $font9
if SCREEN_SIZE.y <= 64: sectionFont = $font5
else if SCREEN_SIZE.y <= 128: sectionFont = $font8

let bodyFont = $font6
if SCREEN_SIZE.y <= 64: sectionFont = $font3
else if SCREEN_SIZE.y <= 128: sectionFont = $font5


enter
────────────────────────────────────────────────────────────────────────
old_transform = get_transform()
old_clip = get_clip()
old_post_effects = get_post_effects()
old_background = get_background()

reset_post_effects()
set_clip(xy(0, 0), SCREEN_SIZE, -2047, 4096)

set_background(window_background_color)


leave
────────────────────────────────────────────────────────────────────────
set_transform(old_transform)
set_clip(old_clip)
set_post_effects(old_post_effects)
set_background(old_background)


frame
────────────────────────────────────────────────────────────────────────

if any_button_press():
   play_sound($cancelUISound)
   pop_mode()

const creditsScroll = max(mode_frames - 70, 0)
set_transform(xy(0, -creditsScroll))

draw_text(sectionFont, CREDITS.title, xy(SCREEN_SIZE.x / 2, SCREEN_SIZE.y / 3), text_color, text_shadow_color, text_shadow_color, 0, 0)
draw_text(bodyFont, "by " + CREDITS.developer, xy(SCREEN_SIZE.x / 2, SCREEN_SIZE.y / 3 + 25), text_color, text_shadow_color, nil, 0, 0)

let y = SCREEN_SIZE.y + 15
for type in creditOrder:
   const s = if type == "Quadplay" then "quadplay✜" else type
   y += draw_text(sectionFont, s + (if type == "Game" or type == "Code" or type == "Quadplay" then "" else "s"), xy(SCREEN_SIZE.x / 2, y), text_color, text_shadow_color, nil, 0, 2).y
   
   for line in CREDITS[lowercase(type)]:
      y += draw_text(bodyFont, line, xy(SCREEN_SIZE.x / 2, y), text_color, nil, nil, 0, 2, 0, SCREEN_SIZE.x).y + 10
      
   y += 20

   
   
   
//for control in gamepad_array:

// TODO

